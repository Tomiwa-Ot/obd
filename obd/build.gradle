plugins {
    id 'com.android.library'
    id 'maven-publish' // Apply the plugin using the id
}

group = "com.github.Tomiwa-Ot" // Moved group outside of final declaration
version = "v1.21" // Moved version outside of final declaration

android {
    namespace "com.grephq.ot.obd"
    compileSdk 34

    defaultConfig {
        minSdk 23
        targetSdk 34

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

afterEvaluate {
    publishing {
        publications {
            // Creates a Maven publication called "release".
            release(MavenPublication) {
                // Applies the component for the release build variant.
                from components.findByName('release')

                // You can then customize attributes of the publication as shown below.
                groupId = project.group
                artifactId = "obd"
                version = project.version
            }

            // Creates a Maven publication called “debug”.
            debug(MavenPublication) {
                // Applies the component for the debug build variant.
                from components.findByName('debug')

                groupId = project.group + ".debug"
                artifactId = "obd-debug"
                version = project.version
            }
        }
        repositories {
            // Configure where to publish your library (e.g., Maven Central, local directory)
            // Example: Publish to a local directory
            maven {
                url "file://${buildDir}/repo"
            }
        }
    }
}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.5.1'
    implementation 'com.google.android.material:material:1.6.1'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
}